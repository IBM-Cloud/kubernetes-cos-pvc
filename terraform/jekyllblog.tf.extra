/*--
docker run --rm -it -p 80:4000 jekyll/builder:$JEKYLL_VERSION bash -c \
  "git clone https://github.com/IBM-Cloud/kubernetes-cos-pvc.git; cd kubernetes-cos-pvc/example/jekyllblog/myblog; mkdir _site; jekyll serve"
--*/


locals {
  blog_pvc_name = "${var.basename}-blog"
}
resource "kubernetes_persistent_volume_claim" "blog" {
  metadata {
    name = local.blog_pvc_name
    annotations = {
      "ibm.io/auto-create-bucket" : "true"
      "ibm.io/auto-delete-bucket" : "false"
      "ibm.io/auto_cache" : "true"
      # "ibm.io/bucket" : "${var.basename}-blog"
      "ibm.io/secret-name" : kubernetes_secret.cos.metadata[0].name
      "ibm.io/set-access-policy" : "false"
    }
  }
  spec {
    storage_class_name = "ibmc-s3fs-standard-regional"
    access_modes       = ["ReadWriteMany"]
    resources {
      requests = {
        storage = "8Gi"
      }
    }
  }
}

resource "kubernetes_deployment" "blog" {
  metadata {
    name = "${var.basename}blog-deployment"
    labels = {
      app = "${var.basename}blog"
    }
  }

  spec {
    replicas = 1

    selector {
      match_labels = {
        app = "${var.basename}blog"
      }
    }

    template {
      metadata {
        labels = {
          app = "${var.basename}blog"
        }
      }

      spec {
        container {
          name    = "blog"
          image   = "jekyll/builder:3.8"
          command = ["sh", "-c", "git clone https://github.com/IBM-Cloud/kubernetes-cos-pvc.git; cd kubernetes-cos-pvc/example/jekyllblog/myblog; mkdir _site; jekyll serve"]
          port {
            container_port = "4000"
          }
          volume_mount {
            name       = "volname"
            mount_path = "/srv/jekyll"
          }
        }
        volume {
          name = "volname"
          persistent_volume_claim {
            claim_name = local.blog_pvc_name
          }
        }
      }
    }
  }
}

resource "kubernetes_service" "blog" {
  metadata {
    name = "${var.basename}blog-service"
    labels = {
      app = "${var.basename}blog"
    }
  }
  spec {
    port {
      port = 4000
    }
    selector = {
      app = "${var.basename}blog"
    }
  }
}


/*------------------------
  ------------------------*/
